---
layout: literate
---
<div class="literate-text">

# Dynamical Decoupling (Simplified)

This example demonstrates a simplified version of dynamical decoupling.
The user is presented with a target state and a current state.
The current state is a quantum state that is subject to decoherence.
The target state is fixed.
The goal of the "game" is to not let the current state deviate too much from the target state.

In this simplified version, the decoherence always rotates around a fixed "zero axis" (the z-axis).
The speed of rotation depends on how aligned the device is with the zero axis (via the inner product
between the zero axis and the IMU gravity measurement).

The user can tap the Qbead to reset the current state to the target state.

</div>

<div class="literate-text">

First, let's include the Qbead library and set up a few useful data structures.

</div>

```cpp class=codeblock
#include <Qbead.h>

Qbead::Qbead bead;

BlochVector current_state(90, 0);
BlochVector target_state(90, 0);
```

<div class="literate-text">

The zero axis is the fixed axis around which decoherence rotates.

</div>

```cpp class=codeblock
BlochVector zero_axis(0, 0);
```

<div class="literate-text">

Prepare some colors for the visualization during the game.

</div>

```cpp class=codeblock
uint32_t purple = color(255, 0, 255);
uint32_t white = color(255, 255, 255);
```

<div class="literate-text">

## Setup

The setup function is called once when the Qbead is powered on and it is used to initialize the Qbead and set up the game.

</div>

```cpp class=codeblock
void setup() {
  bead.begin();
  bead.setBrightness(25);
```

<div class="literate-text">

Test the pixels by flashing a colorful pattern to make sure they are working.

</div>

```cpp class=codeblock
  bead.testPixels();
}
```

<div class="literate-text">

## Event loop

The loop function is called repeatedly until the Qbead is powered off.
It is used to read the IMU and update the current state of the game.

</div>

```cpp class=codeblock
void loop() {
  static bool current_state_visible = true;
```

<div class="literate-text">

Read the IMU to get the current gravity direction.

</div>

```cpp class=codeblock
  bead.readIMU(false);
```

<div class="literate-text">

Clear the display.

</div>

```cpp class=codeblock
  bead.clear();
```

<div class="literate-text">

### Draw the "reference" state -- the one corresponding to no decoherence.

</div>

```cpp class=codeblock
  bead.setBloch_deg(target_state, white);
```

<div class="literate-text">

### Draw the current state, as it evolves over time under the influence of decoherence.

</div>

<div class="literate-text">

The current state is only visible when the user has tapped to reveal it.
Each tap toggles the visibility on or off.

</div>

```cpp class=codeblock
  if (current_state_visible) {
    bead.setBloch_deg(current_state, purple);
  }
```

<div class="literate-text">

Show the result.

</div>

```cpp class=codeblock
  bead.show();
```

<div class="literate-text">

### Simulate the decoherence

The decoherence is simulated by rotating the current state around the fixed zero axis.
The rotation speed depends on the inner product between the zero axis and the IMU measurement.

</div>

```cpp class=codeblock
  float rotation_speed = 0.2 * innerProductGeom(zero_axis, BlochVector(bead.x, bead.y, bead.z));
  current_state.rotateAround(zero_axis, rotation_speed);
```

<div class="literate-text">

### Check for taps

If the user taps the Qbead, toggle the visibility of the current state
and reset the current state to the target state.

</div>

```cpp class=codeblock
  if (bead.wasTapped()) {
    Serial.println("TAP");
    current_state_visible = !current_state_visible;
  }

  current_state_visible = current_state_visible || (millis() < 5000);

  Serial.print(millis());
  Serial.print("  | Visible: ");
  Serial.print(current_state_visible);
  Serial.print(" | Angle: ");
  Serial.print(current_state.phi);
  Serial.print(" | Speed: ");
  Serial.println(rotation_speed);
}
```
